apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    final sdk = rootProject.ext.sdk
    signingConfigs {
        debug {
            storeFile file("external/debug.keystore")
        }
    }
    compileSdkVersion sdk.compileSdk
    defaultConfig {
        applicationId "com.gee12.mytetroid"
        minSdkVersion sdk.minSdk
        targetSdkVersion sdk.targetSdk
        versionCode 64
        versionName "5.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'default'
    productFlavors {
        free {
            dimension = 'default'
            buildConfigField "String", "DEF_APPLICATION_ID", '"'+android.defaultConfig.applicationId+'"'
        }
        pro {
            dimension = 'default'
            applicationIdSuffix = '.pro'
            buildConfigField "String", "DEF_APPLICATION_ID", '"'+android.defaultConfig.applicationId+'"'
        }
        googlePlayFree {
            dimension = 'default'
            buildConfigField "String", "DEF_APPLICATION_ID", '"'+android.defaultConfig.applicationId+'"'
        }
        googlePlayPro {
            dimension = 'default'
            applicationIdSuffix = '.pro'
            buildConfigField "String", "DEF_APPLICATION_ID", '"'+android.defaultConfig.applicationId+'"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.getFlavorName()
            def flavorName = ""
            if (flavor == "pro" || flavor == "googlePlayPro") {
                flavorName = "_Pro"
            }
            def postfix = ""
            if (flavor == "googlePlayFree" || flavor == "googlePlayPro") {
                postfix = "_GP"
            }
            outputFileName = "MyTetroid${flavorName}_${variant.versionName}${postfix}.apk"
        }
    }
}


dependencies {
    def lifecycle_version = "2.5.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.9.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation (project(path: ':wysiwyg-editor')) {
        exclude group: 'org.jetbrains', module: 'annotations'
    }
    implementation project(path: ':multi-listview')
    implementation project(path: ':pinlock-view')
    implementation project(path: ':image-picker')
    implementation project(path: ':simple-storage')
    implementation 'org.ini4j:ini4j:0.5.4'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'org.jdom:jdom2:2.0.6'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.github.clans:fab:1.6.4'
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"

    implementation 'androidx.webkit:webkit:1.8.0'

    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation "io.insert-koin:koin-android:3.2.0"
}
repositories {
    mavenCentral()
}
